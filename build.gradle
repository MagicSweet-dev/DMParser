import org.apache.tools.ant.filters.ReplaceTokens
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

group = 'com.magicsweet'
version = '1.0-beta'

sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name 'commandapi'
        url 'https://raw.githubusercontent.com/JorelAli/CommandAPI/mvn-repo/'
    }
    maven {
        name 'codemc-repo'
        url 'https://repo.codemc.org/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'

    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'


    compileOnly 'dev.jorel:commandapi-core:5.6'
    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.7.1'

    compile 'org.spongepowered:configurate-yaml:4.0.0'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

shadowJar {
    dependencies {
        include(dependency('org.spongepowered:configurate-yaml:.*'))
        include(dependency('org.spongepowered:configurate-core:.*'))
        include(dependency('io.leangen.geantyref:geantyref:1.+'))
        include(dependency('org.yaml:snakeyaml:1.+'))
    }
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = 'shadow'
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar
